#
# Copyright (C) 2014 Alex Smith
#
# Permission to use, copy, modify, and/or distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
#

Import('config', 'defaults', 'env')
from util import FeatureSources

sources = FeatureSources(config, [
    'lib/string.c',

    'arch.c',
    'descriptor.c',
    'entry.S',
    'exception.c',
    ('X86_TSC', 'time.c'),
])

# Set 64-/32-bit-specific flags.
if config['64BIT']:
    env['CCFLAGS'] += ['-m64', '-mcmodel=small', '-mno-red-zone']
    env['ASFLAGS'] += ['-m64']
    env['LINKFLAGS'] += ['-m64']
else:
    env['CCFLAGS'] += ['-m32']
    env['ASFLAGS'] += ['-m32']
    env['LINKFLAGS'] += ['-m32']

    # 32-bit needs some support functions from compiler-rt.
    sources += map(lambda x : File('../../lib/runtime/' + x), [
        'i386/udivdi3.S',
        'i386/umoddi3.S',
        'i386/divdi3.S',
        'i386/moddi3.S',
    ])

# Prevent FPU code generation.
env['CCFLAGS'] += ['-mno-sse', '-mno-mmx', '-mno-sse2', '-mno-3dnow']

# Warn on stack frames that are too large.
env['CCFLAGS'] += ['-Wframe-larger-than=512']

# Default page size may be larger which can result in a large output binary.
env['LINKFLAGS'] += ['-Wl,-z,max-page-size=0x1000', '-Wl,-z,common-page-size=0x1000']

Return('sources')
