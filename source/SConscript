#
# Copyright (C) 2014 Alex Smith
#
# Permission to use, copy, modify, and/or distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
#

vars = ['config', 'defaults', 'env']

Import(*vars)
from util import FeatureSources

sources = FeatureSources(config, [
    ('TARGET_HAS_DISK', 'fs/ext2.c'),
    ('TARGET_HAS_DISK', 'fs/iso9660.c'),

    'lib/printf.c',
    'lib/qsort.c',
    'lib/string.c',

    ('TARGET_HAS_DISK', 'partition/gpt.c'),
    ('TARGET_HAS_DISK', 'partition/mbr.c'),

    'config.c',
    'console.c',
    'device.c',
    ('TARGET_HAS_DISK', 'disk.c'),
    'error.c',
    'fs.c',
    'main.c',
    'memory.c',
    'shell.c',
    ('TARGET_HAS_UI', 'ui.c'),
    ('TARGET_HAS_VIDEO', 'video.c'),
])

# Set the include search paths.
env['CPPPATH'] = [
    Dir('include'),
    Dir('arch/%s/include' % (config['ARCH'])),
    Dir('platform/%s/include' % (config['PLATFORM'])),
]

# Generate the configuration header.
f = open('config.h', 'w')
f.write('/* This file is automatically-generated, do not edit. */\n\n')
for (k, v) in config.items():
    if isinstance(v, str):
        f.write("#define CONFIG_%s \"%s\"\n" % (k, v))
    elif isinstance(v, bool):
        if v:
            f.write("#define CONFIG_%s 1\n" % (k))
    elif isinstance(v, int):
        f.write("#define CONFIG_%s %d\n" % (k, int(v)))
    else:
        raise Exception, "Unsupported type %s in config" % (type(v))
f.close()

# Include the generated configuration header.
env['CCFLAGS'] += ['-include', 'build/%s/config.h' % (env['CONFIG'])]
env['ASFLAGS'] += ['-include', 'build/%s/config.h' % (env['CONFIG'])]

# Platform code needs the location of the loader binary.
env['LOADER'] = File('loader.elf')

# We place the final output binaries in a single directory.
env['OUTDIR'] = Dir('bin')

# Get the list of source files from the architecture/platform.
arch_sources = SConscript(dirs = ['arch/' + config['ARCH']], exports = vars)
platform_sources = SConscript(dirs = ['platform/' + config['PLATFORM']], exports = vars)
drivers_sources = SConscript(dirs = ['drivers'], exports = vars)
sources = arch_sources + platform_sources + drivers_sources + sources
objects = map(env.Object, sources)

# Link the loader.
linkflags = env['LINKFLAGS'] + ['-Wl,-T,$LDSCRIPT']
target = env.Program('loader.elf', objects, LINKFLAGS = linkflags)
Depends(target, env['LDSCRIPT'])

# Generate various information files.
env.AddPostAction(target, [
    Action('$OBJDUMP -d $TARGET > ${TARGET.base}.lst', None),
    Action('$READELF -a $TARGET > ${TARGET.base}.rde', None),
    Action('$NM -C $TARGET | sort > ${TARGET.base}.sym', None),
])
