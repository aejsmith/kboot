/*
 * SPDX-FileCopyrightText: (C) Alex Smith <alex@alex-smith.me.uk>
 * SPDX-License-Identifier: ISC
 */

/**
 * @file
 * @brief               BIOS loader linker script.
 */

#include <arch/page.h>

#include <platform/loader.h>

OUTPUT_FORMAT("elf32-i386")
OUTPUT_ARCH("i386")
ENTRY(loader_entry)

SECTIONS {
    . = LOADER_LOAD_ADDR;
    __start = .;

    /* Init code in start.S must be placed first: execution begins from the very
     * start of the loader image. */
    .init.text : { *(.init.text) }
    .init.data : { *(.init.data) }

    .text ALIGN(32) : {
        *(.text)
        *(.text.*)
    }

    .rodata ALIGN(32) : {
        *(.rodata)
        *(.rodata.*)
    }

    .data ALIGN(32) : {
        *(.data)
        *(.data.*)

        __builtins_start = .;
        *(.builtins)
        __builtins_end = .;

        __data_end = .;
    }

    .bss ALIGN(32) : {
        __bss_start = .;
        *(.bss)
        *(.bss.*)
        *(COMMON)
        __bss_end = .;
    }

    . = ALIGN(PAGE_SIZE);
    __end = .;

    /DISCARD/ : {
        *(.note.*)
        *(.comment*)
        *(.eh_frame*)
    }
}
