#
# Copyright (C) 2015 Alex Smith
#
# Permission to use, copy, modify, and/or distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
#

Import('config', 'env')
from util import FeatureSources

sources = FeatureSources(config, [
    'relocate.c',
    'start.S',
])

# Set the linker script path.
env['LDSCRIPT'] = env.LDScript('link.ld', 'link.ld.in')

# The EFI binary must be relocatable, so on 64-bit this means we must build
# position independent code.
env['CCFLAGS'] += ['-fpic']
env['ASFLAGS'] += ['-fpic']

# Build the final EFI binary.
env.Command(
    env['OUTDIR'].File('kbootx64.efi'),
    env['KBOOT'],
    Action(
        '$OBJCOPY -j .text -j .reloc -j .data -j .dynamic -j .rela ' +
            '-j .dynsym -j .bss --target=efi-app-x86_64 $SOURCE $TARGET',
        '$GENCOMSTR'))

Return('sources')
